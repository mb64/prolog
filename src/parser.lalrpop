use crate::parser::{Tok, Expr, Clause, ReplItem, Span};
use lasso::{Rodeo, Spur};

grammar<'input, 'rodeo>(input: &'input str, rodeo: &'rodeo mut Rodeo, file_id: u32);

extern {
    type Location = usize;
    type Error = ();

    enum Tok<'input> {
        "(" => Tok::LParen,
        ")" => Tok::RParen,
        "[" => Tok::LBracket,
        "]" => Tok::RBracket,
        "|" => Tok::Bar,

        "." => Tok::Period,
        "," => Tok::Comma,
        "?" => Tok::Question,
        ":-" => Tok::Turnstile,

        "\\+" => Tok::WeirdNot,
        "=" => Tok::Equals,
        "\\=" => Tok::NotEquals,
        is => Tok::Is,
        "<" => Tok::LessThan,
        ">" => Tok::GreaterThan,
        "+" => Tok::Plus,
        "-" => Tok::Minus,
        "*" => Tok::Times,
        "/" => Tok::Divide,

        "_" => Tok::Wildcard,
        functor => Tok::Functor(<&'input str>),
        Variable => Tok::Variable(<&'input str>),
        number => Tok::Number(<i64>),
        string => Tok::String(<&'input str>),
    }
}

pub Program: Vec<Clause> = Clause*;
pub ReplItem: ReplItem = {
    <Clause*> => ReplItem::Clauses(<>),
    <Question> => ReplItem::Question(<>),
}

Question = <Exprs> "?";

Clause: Clause = {
    <l:@L> <f:Functor> "." <r:@R> => {
        let span = Span::new(file_id, l, r);
        let (_, functor, args) = f;
        Clause { span, functor, args, subgoals: vec![] }
    },
    <l:@L> <f:Functor> ":-" <subgoals:Exprs> "." <r:@R> => {
        let span = Span::new(file_id, l, r);
        let (_, functor, args) = f;
        Clause { span, functor, args, subgoals }
    },
}

// Allow trailing commas bc why not
Exprs: Vec<Expr> = {
    <h:(<Expr> ",")*> <t:Expr?> =>
        h.into_iter().chain(t).collect()
}

Expr: Expr = {
    CmpExpr,
    <l:@L> "\\+" <e:Expr> <r:@R> => Expr::Functor {
        span: Span::new(file_id, l, r),
        name: rodeo.get_or_intern("\\+"),
        args: vec![e],
    }
}

Operator<Left,Op,Right>: Expr = {
    Right,
    <l:@L> <lhs:Left> <op:Op> <rhs:Right> <r:@R> => Expr::Functor {
        span: Span::new(file_id, l, r),
        name: rodeo.get_or_intern(op),
        args: vec![lhs, rhs],
    },
}

CmpExpr = Operator<AddExpr, CmpOp, AddExpr>;
AddExpr = Operator<AddExpr, AddOp, UnaryExpr>;
UnaryExpr = {
    MulExpr,
    <l:@L> <op:AddOp> <e:UnaryExpr> <r:@R> => Expr::Functor {
        span: Span::new(file_id, l, r),
        name: rodeo.get_or_intern(op),
        args: vec![e],
    },
}
MulExpr = Operator<MulExpr, MulOp, Term>;

Term: Expr = {
    <l:@L> "(" <inner:Expr> ")" <r:@R> => {
        let span = Span::new(file_id, l, r);
        Expr::Paren { span, inner: Box::new(inner) }
    },
    <l:@L> "_" <r:@R> => {
        let span = Span::new(file_id, l, r);
        Expr::Wildcard { span }
    },
    <l:@L> <value:number> <r:@R> => {
        let span = Span::new(file_id, l, r);
        Expr::Number { span, value }
    },
    <l:@L> <v:Variable> <r:@R> => {
        let span = Span::new(file_id, l, r);
        Expr::Var { span, name: rodeo.get_or_intern(v) }
    },
    Functor => {
        let (span, name, args) = <>;
        Expr::Functor { span, name, args }
    },
    <l:@L> <s:string> <r:@R> => {
        let span = Span::new(file_id, l, r);
        Expr::String { span, value: s.to_owned() }
    },
    List,
}

List: Expr = {
    <l:@L> "[" <items:Exprs> "]" <r:@R> => {
        let span = Span::new(file_id, l, r);
        Expr::List { span, items, tail: None }
    },

    <l:@L> "[" <items:Exprs> "|" <tail:Expr> "]" <r:@R> => {
        let span = Span::new(file_id, l, r);
        Expr::List { span, items, tail: Some(Box::new(tail)) }
    },
}

Functor: (Span, Spur, Vec<Expr>) = {
    <l:@L> <f:functor> "(" <args:Exprs> ")" <r:@R> => {
        let span = Span::new(file_id, l, r);
        (span, rodeo.get_or_intern(f), args)
    },
    <l:@L> <f:functor> <r:@R> => {
        let span = Span::new(file_id, l, r);
        (span, rodeo.get_or_intern(f), vec![])
    },
}


// Operators

CmpOp: &'static str = {
    "=" => "=",
    "\\=" => "\\=",
    is => "is",
    "<" => "<",
    ">" => ">",
}

AddOp: &'static str = {
    "+" => "+",
    "-" => "-",
}

MulOp: &'static str = {
    "*" => "*",
    "/" => "/",
}
